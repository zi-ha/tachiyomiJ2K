plugins {
    id(Plugins.kotlinter.name) version Plugins.kotlinter.version
    id(Plugins.gradleVersions.name) version Plugins.gradleVersions.version
    id(Plugins.jetbrainsKotlin) version AndroidVersions.kotlin apply false
}
allprojects {
    repositories {
        maven {
            setUrl("https://maven.pkg.jetbrains.space/kotlin/p/kotlin/gradle")
            content {
                includeGroupByRegex("org\\.jetbrains\\.kotlin(\\..*)?")
            }
        }
        maven {
            setUrl("https://maven.aliyun.com/repository/google")
        }
        maven {
            setUrl("https://maven.aliyun.com/repository/central")
        }
        maven {
            setUrl("https://maven.aliyun.com/repository/jitpack")
            content {
                // Avoid Kotlin plugin artifacts going to Aliyun jitpack mirror
                excludeGroupByRegex("org\\.jetbrains\\.kotlin(\\..*)?")
            }
        }
        google()
        mavenCentral()
        maven { setUrl("https://jitpack.io") }
        maven { setUrl("https://plugins.gradle.org/m2/") }
    }
}

subprojects {
    apply(plugin = Plugins.kotlinter.name)

    kotlinter {
//        experimentalRules = true

        // Doesn't play well with Android Studio
//        disabledRules = arrayOf("experimental:argument-list-wrapping")
    }
}

buildscript {
    repositories {
        maven {
            setUrl("https://maven.pkg.jetbrains.space/kotlin/p/kotlin/gradle")
            content {
                includeGroupByRegex("org\\.jetbrains\\.kotlin(\\..*)?")
            }
        }
        maven {
            setUrl("https://maven.aliyun.com/repository/gradle-plugin")
            content {
                excludeGroupByRegex("org\\.jetbrains\\.kotlin(\\..*)?")
            }
        }
        maven { setUrl("https://maven.aliyun.com/repository/google") }
        maven { setUrl("https://maven.aliyun.com/repository/central") }
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        //noinspection AndroidGradlePluginVersion
        classpath("com.android.tools.build:gradle:8.7.3")
        classpath("com.google.gms:google-services:4.4.4")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${AndroidVersions.kotlin}")
        classpath("com.google.android.gms:oss-licenses-plugin:0.10.10")
        classpath("org.jetbrains.kotlin:kotlin-serialization:${AndroidVersions.kotlin}")
        classpath("com.google.firebase:firebase-crashlytics-gradle:3.0.6")
    }
}

tasks.named("dependencyUpdates", com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask::class.java).configure {
    rejectVersionIf {
        isNonStable(candidate.version)
    }
    // optional parameters
    checkForGradleUpdate = true
    outputFormatter = "json"
    outputDir = "build/dependencyUpdates"
    reportfileName = "report"
}

tasks.register("clean", Delete::class) {
    delete(rootProject.buildDir)
}
